# Maintainer: Bernhard Landauer <oberon@manjaro.org>
# Maintainer: Thorsten TÃ¶pper <atsutane-tu@freethoughts.de>

_pkgname=i3status
pkgname=$_pkgname-manjaro
pkgver=2.12
pkgrel=1
pkgdesc='customized status bar for manjaro-i3, dzen2 or xmobar'
arch=('i686' 'x86_64')
groups=('i3-manjaro')
url='https://github.com/i3/i3status'
license=('BSD')
depends=('confuse'
	'libpulse'
	'libnl'
	'ttf-dejavu'
    'ttf-font-icons'
	'wireless_tools'
	'yajl')
makedepends=('asciidoc'
	# 'git'
	)
options=('docs')
conflicts=('i3status')
provides=('i3status')
backup=('etc/i3status.conf')
install=$pkgname.install
source=("$url/archive/$pkgver.tar.gz"
	'manjaro.diff')
md5sums=('15be9a20389d1130116100d0d263812a'
         '59842c3318c2f430e5934daa807c53c8')

prepare() {
  cd $_pkgname-$pkgver
  patch -p0 < ../manjaro.diff
}

build() {
  cd $_pkgname-$pkgver
  make
}

package() {
  cd $_pkgname-$pkgver
  make DESTDIR="$pkgdir" install
  install -Dm644 LICENSE $pkgdir/usr/share/licenses/${_pkgname}/LICENSE
  make clean
}
