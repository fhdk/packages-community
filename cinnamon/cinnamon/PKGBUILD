# Maintainer: Bernhard Landauer <oberon@manjaro.org>
# Maintainer: Alexandre Filgueira <alexfilgueira@cinnarch.com>

pkgname=cinnamon
pkgver=3.8.2
pkgrel=2
pkgdesc="Linux desktop which provides advanced innovative features and a traditional user experience"
arch=('i686' 'x86_64')
url="https://github.com/linuxmint/Cinnamon"
license=('GPL2')
depends=('accountsservice'
    'caribou'
    'cinnamon-control-center'
    'cinnamon-menus'
    'cinnamon-screensaver'
    'cinnamon-session'
    'cinnamon-settings-daemon'
    'cjs'
    'clutter-gtk'
    'gettext'
    'gnome-backgrounds'
    'gnome-themes-standard'
    'gstreamer'
    'iso-flag-png'
    'libgnomekbd'
    'libkeybinder3'
    'librsvg'
    'metacity'
    'muffin>=3.8'
    'nemo'
    'network-manager-applet'
    'polkit-gnome'
    'python-cairo'
    'python-dbus'
    'python-gobject'
    'python-pam'
    'python-pexpect'
    'python-pillow'
    'python-pyinotify'
    'tint2'
    'xapps'
    'xdg-utils')
makedepends=('gnome-common'
    'intltool'
    'gobject-introspection')
optdepends=('blueberry: Bluetooth support'
    'cinnamon-translations: i18n'
    'gnome-panel: fallback mode'
#    'metacity: fallback mode'
    'system-config-printer: printer settings'
#    'tint2: fallback mode'
)
options=('!emptydirs')
source=("$pkgname-$pkgver::$url/archive/$pkgver.tar.gz"
    "cinanmon-settings-don-t-rely-on-the-presence-of-cinn.patch::$url/pull/7382.patch"
    "set_wheel.patch"
    "default-theme.patch"
    'add-msm.patch')
sha256sums=('0464f072341b4d19aeed9f66637a4a0b86e1f2d67a6101f032db245d35accff2'
            'd46b2d6c60b52aa87f5a30568c2106d77b45c0febe8bf387ffd201b5b4f5a101'
            '7517d651d440361947f9539fe8f42548d5eb43a09c28c9a11f51cfdfdefd042f'
            'bb60a2bb27efbef132798677f61ac46aa6d17f5432f0f81117ec97c1b4601aad'
            'a793089112b1de3046e11334bc5581785961cc1eb7c33d437dec114dac498b0d')
            
prepare() {
  cd "${srcdir}"/Cinnamon-${pkgver}

  # Check for the cc-panel module path, not for the irrelevant binary
  patch -p1 -i ../cinanmon-settings-don-t-rely-on-the-presence-of-cinn.patch

  # Use wheel group instread of sudo (taken from Fedora)
  patch -Np1 -i ../set_wheel.patch

  # Set default theme to 'cinnamon'
  patch -Np1 -i ../default-theme.patch
  
  # add MSM to cinnamon-settings
  patch -Np1 -i ../add-msm.patch

  # Replace MintInstall with GNOME Software
  sed -i 's/mintinstall.desktop/org.gnome.Software.desktop/' data/org.cinnamon.gschema.xml.in

  # Add polkit agent to required components
  sed -i 's/RequiredComponents=\(.*\)$/RequiredComponents=\1polkit-gnome-authentication-agent-1;/' \
    files/usr/share/cinnamon-session/sessions/cinnamon*.session

  # Cinnamon has no upstream backgrounds, use GNOME backgrounds instead
  sed -i 's|/usr/share/cinnamon-background-properties|/usr/share/gnome-background-properties|' \
    files/usr/share/cinnamon/cinnamon-settings/modules/cs_backgrounds.py

  NOCONFIGURE=1 ./autogen.sh
}

build() {
  cd ${pkgname^}-$pkgver
  ./configure --prefix=/usr \
    --sysconfdir=/etc \
    --libexecdir=/usr/lib/cinnamon \
    --localstatedir=/var \
    --disable-static \
    --disable-gtk-doc \
    --disable-schemas-compile \
    --enable-compile-warnings=yes

  #https://bugzilla.gnome.org/show_bug.cgi?id=656231
  sed -i -e 's/ -shared / -Wl,-O1,--as-needed\0/g' libtool

  make
}

package() {
  cd ${pkgname^}-$pkgver
  make DESTDIR="${pkgdir}" install

  # enable Adwaita themes
  mkdir -p $pkgdir/usr/share/themes/Adwaita{,-dark}/metacity-1/
  echo 'native' > $pkgdir/usr/share/themes/Adwaita/metacity-1/muffin-adwaita.txt
  echo 'native' > $pkgdir/usr/share/themes/Adwaita-dark/metacity-1/muffin-adwaita.txt

  # workaround for lightdm-settings launcher
  sed -i 's/lightdm-settings\",                    /ppkexec lightdm-settings\",             /' $pkgdir/usr/share/cinnamon/cinnamon-settings/cinnamon-settings.py
}
