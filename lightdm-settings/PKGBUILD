# Maintainer: Stefano Capitani <stefanoatmanjarodotorg>
# Maintainer: Bernhard Landauer <oberon@manjaro.org>

pkgname=lightdm-settings
pkgver=1.2.0
pkgrel=1
pkgdesc="A configuration tool for the LightDM display manager "
_add_group=add-autologin-group
arch=('any')
url="https://github.com/linuxmint/${pkgname}"
license=('GPL3')
depends=('hicolor-icon-theme'
    'lightdm-slick-greeter'
    'polkit'
    'python-gobject'
    'python-setproctitle'
    'python-xapp')
makedepends=('desktop-file-utils' 'gettext')
install=$_add_group.install
source=("$url/archive/$pkgver.tar.gz"
    'user_background.patch'
    "$_add_group.script"
    "$_add_group.service")
sha256sums=('7bf9ece76dc3f28b418bc68313de365e265cd6d50a27a5b3157d37e45e511e17'
            '00cb1a34dba3c3300fc81e0bacf8f5806c0d8937de35c021e50430de5a8acdc4'
            '475bb4805a419aca106f9f42bcb837c7dc53eeb84b5c50d8ee8d892b55d6c2b4'
            'a356ee9dc1e876d73c49ab30fdc6417edb7a817639122fff7fe0ebff72e33190')

prepare() {
  cd $pkgname-$pkgver
  patch -p1 -i ../user_background.patch
}

build() {
  cd $pkgname-$pkgver
  make
}

package() {
  cd $pkgname-$pkgver
  cp -r usr $pkgdir
  
#install systemd service
  cd /$srcdir
  install -Dm755 $_add_group.script $pkgdir/usr/bin/$_add_group
  install -Dm644 $_add_group.service $pkgdir/etc/systemd/system/$_add_group.service
}
